IF
  Global("SaveSavedLoc","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("SaveSavedLoc","LOCALS",1)
    SaveObjectLocation("LOCALS","SavedLoc",Myself)
    Continue()
END

IF
	!BitCheck("AREA_VAR","MYAREA",1)
	Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		BitSet("AREA_VAR","MYAREA",1)
		Continue()
END

IF
	StateCheck(Myself,STATE_PANIC)
THEN
	RESPONSE #100
		RandomWalkContinuous()
END

IF
	!GlobalTimerNotExpired("IWD_Rest","LOCALS")
THEN
	RESPONSE #100
		SetGlobalTimer("IWD_Rest","LOCALS",EIGHT_HOURS)
		ApplySpell(Myself,RESTORE_FULL_HEALTH)
		Rest()
END

IF
	HaveSpell(CLERIC_PROTECT_FROM_EVIL)
	BitCheck("AREA_VAR","MYAREA",1)
THEN
	RESPONSE #100
		MoveToPoint([1677.603])
		Face(SW)
		Spell(Myself,CLERIC_BLESS)
		Wait(2)
		Spell(Myself,CLERIC_PROTECT_FROM_EVIL)
		Continue()
END

IF
	HaveSpell(CLERIC_HOLD_PERSON)
	OR(2)
		See(NearestEnemyOf(Myself))
		See([PC.0.0.FIGHTER])
	!StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
	BitCheck("AREA_VAR","MYAREA",1)
	Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),CLERIC_HOLD_PERSON)
		Wait(2)
END

IF
	HaveSpell(CLERIC_SILENCE_15_FOOT)
	See([PC.0.0.MAGE])
	!StateCheck(LastSeenBy(Myself),STATE_SILENCED)
	BitCheck("AREA_VAR","MYAREA",1)
	Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),CLERIC_SILENCE_15_FOOT)
		Wait(2)
END

IF
	HaveSpell(CLERIC_GLYPH_OF_WARDING)
	See(NearestEnemyOf(Myself))
	BitCheck("AREA_VAR","MYAREA",1)
THEN
	RESPONSE #100
		Help()
		Spell(LastSeenBy(Myself),CLERIC_GLYPH_OF_WARDING)
		Wait(2)
END

IF
	BitCheck("AREA_VAR","MYAREA",1)
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		Help()
		AttackReevaluate(LastSeenBy(Myself),75)
END

IF
	BitCheck("AREA_VAR","MYAREA",1)
	AttackedBy(NearestEnemyOf(Myself),DEFAULT)
THEN
	RESPONSE #100
		FaceObject(LastAttackerOf(Myself))
		Continue()
END

IF
	BitCheck("AREA_VAR","MYAREA",1)
	Help(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		FaceObject(LastHelp(Myself))
		Continue()
END

IF
	!BitCheck("AREA_VAR","MYAREA",1)
	AttackedBy([GOODCUTOFF],DEFAULT)
THEN
	RESPONSE #100
		BitSet("AREA_VAR","MYAREA",1)
		Enemy()
		Continue()
END

IF
	NumTimesTalkedTo(0)
	See([PC])
	!Allegiance(Myself,ENEMY)
	!StateCheck(Myself,STATE_REALLY_DEAD)
THEN
	RESPONSE #100
		ApplySpellRES("BDCUTSPY",Myself)
		StartDialogNoSet([PC])
END

IF
	ActionListEmpty()
	!NearSavedLocation(Myself,"LOCALSSavedLoc",24)
THEN
	RESPONSE #100
		MoveToSavedLocation("SavedLoc","LOCALS")
END

