IF
  Global("SaveSavedLoc","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("SaveSavedLoc","LOCALS",1)
    SaveObjectLocation("LOCALS","SavedLoc",Myself)
    Continue()
END

IF
	StateCheck(Myself,STATE_PANIC)
THEN
	RESPONSE #100
		RandomWalkContinuous()
END

IF
	HasItem("POTN08",Myself) // Лечебное зелье
	HPPercentLT(Myself,50)
THEN
	RESPONSE #100
		UseItem("POTN08",Myself) // Лечебное зелье
		Continue()
END

IF
	HasItem("POTN52",Myself) // Зелье Усиленного лечения
	HPPercentLT(Myself,50)
THEN
	RESPONSE #100
		UseItem("POTN52",Myself) // Зелье Усиленного лечения
		Continue()
END

IF
	See(NearestEnemyOf(Myself))
	!Range(LastSeenBy(Myself),4)
	CanEquipRanged()
THEN
	RESPONSE #100
		EquipRanged()
		AttackReevaluate(LastSeenBy(Myself),45)
END

IF
	See(NearestEnemyOf(Myself))
	Range(LastSeenBy(Myself),4)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		AttackReevaluate(LastSeenBy(Myself),45)
END

IF
	AttackedBy(NearestEnemyOf(Myself),DEFAULT)
THEN
	RESPONSE #100
		MoveToObject(LastAttackerOf(Myself))
END

IF
	Help([ENEMY])
THEN
	RESPONSE #100
		MoveToObject(NearestEnemyOf(LastHelp(Myself)))
END

IF
	AttackedBy([GOODCUTOFF],DEFAULT)
THEN
	RESPONSE #100
		Enemy()
		Continue()
END

IF
	ActionListEmpty()
	!NearSavedLocation(Myself,"LOCALSSavedLoc",36)
THEN
	RESPONSE #100
		MoveToSavedLocation("SavedLoc","LOCALS")
END

IF
	ActionListEmpty()
THEN
	RESPONSE #100
		RandomWalk()
		Wait(5)
END

