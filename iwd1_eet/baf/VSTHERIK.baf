IF
  Global("SaveSavedLoc","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("SaveSavedLoc","LOCALS",1)
    SaveObjectLocation("LOCALS","SavedLoc",Myself)
    Continue()
END

IF
	!BitCheck("AREA_VAR","MYAREA",4)
	Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		BitSet("AREA_VAR","MYAREA",4)
		Continue()
END

IF
	StateCheck(Myself,STATE_PANIC)
THEN
	RESPONSE #100
		RandomWalkContinuous()
END

IF
	BitCheck("AREA_VAR","MYAREA",4)
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		Help()
		AttackReevaluate(LastSeenBy(Myself),75)
END

IF
	BitCheck("AREA_VAR","MYAREA",4)
	AttackedBy(NearestEnemyOf(Myself),DEFAULT)
THEN
	RESPONSE #100
		Help()
		AttackReevaluate(LastAttackerOf(Myself),75)
END

IF
	AttackedBy([GOODCUTOFF],DEFAULT)
THEN
	RESPONSE #100
		BitSet("AREA_VAR","MYAREA",4)
		Enemy()
		Continue()
END

IF
	NumTimesTalkedTo(0)
	!Allegiance(Myself,ENEMY)
	See([PC])
	!StateCheck(Myself,STATE_REALLY_DEAD)
THEN
	RESPONSE #100
		StartCutSceneMode()
		ClearAllActions()
		MultiPlayerSync()
		ApplySpellRES("BDCUTSPY",Myself)
		StartDialogNoSet([PC])
END

IF
	ActionListEmpty()
	!NearSavedLocation(Myself,"LOCALSSavedLoc",0)
THEN
	RESPONSE #100
		MoveToSavedLocation("SavedLoc","LOCALS")
END

