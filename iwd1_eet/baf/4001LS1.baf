IF
	StateCheck(Myself,STATE_PANIC)
THEN
	RESPONSE #100
		RandomWalkContinuous()
END

IF
	!GlobalTimerNotExpired("IWD_Rest","LOCALS")
THEN
	RESPONSE #100
		SetGlobalTimer("IWD_Rest","LOCALS",EIGHT_HOURS)
		ApplySpell(Myself,RESTORE_FULL_HEALTH)
		Rest()
END

IF
	HaveSpell(CLERIC_CURE_LIGHT_WOUNDS)
	See("LIZARD_KING")
	!StateCheck(LastSeenBy(Myself),STATE_REALLY_DEAD)
	HPPercentLT(LastSeenBy(Myself),50)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),CLERIC_CURE_LIGHT_WOUNDS)
		Wait(3)
		Continue()
END

IF
	HaveSpell(CLERIC_STRENGTH_OF_ONE)
	See("LIZARD_KING")
	Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),CLERIC_STRENGTH_OF_ONE)
		Wait(2)
END

IF
	HaveSpell(CLERIC_HOLD_PERSON)
	OR(3)
		See([PC.0.0.RANGER_ALL])
		See([PC.0.0.PALADIN_ALL])
		See([PC.0.0.FIGHTER_ALL])
	!StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
	Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),CLERIC_HOLD_PERSON)
		Wait(2)
END

IF
	HaveSpell(CLERIC_SILENCE_15_FOOT)
	OR(3)
		See([PC.0.0.CLERIC_ALL])
		See([PC.0.0.MAGE_ALL])
		See([PC.0.0.DRUID_ALL])
	!StateCheck(LastSeenBy(Myself),STATE_SILENCED)
	Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),CLERIC_SILENCE_15_FOOT)
		Wait(2)
END

