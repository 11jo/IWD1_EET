IF
  Global("SaveSavedLoc","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("SaveSavedLoc","LOCALS",1)
    SaveObjectLocation("LOCALS","SavedLoc",Myself)
    Continue()
END

IF
	StateCheck(Myself,STATE_PANIC)
THEN
	RESPONSE #100
		RandomWalkContinuous()
END

IF
	!GlobalTimerNotExpired("IWD_Rest","LOCALS")
THEN
	RESPONSE #100
		SetGlobalTimer("IWD_Rest","LOCALS",EIGHT_HOURS)
		ApplySpell(Myself,RESTORE_FULL_HEALTH)
		Rest()
END

IF
	//HaveSpell(CLERIC_DEFENSIVE_HARMONY)  // SPPR406.SPL (Защитная гармония)
	Global("CDCLERIC_DEFENSIVE_HARMONY","LOCALS",0)
	Range("YXUNOMEI",15)
	!Global("YXUN_ATTACK","MYAREA",0)
THEN
	RESPONSE #100
		SetGlobal("CDCLERIC_DEFENSIVE_HARMONY","LOCALS",1)
		ForceSpell(Myself,CLERIC_DEFENSIVE_HARMONY)  // SPPR406.SPL (Защитная гармония)
		Wait(2)
END

IF
	//HaveSpell(CLERIC_CHANT)  // SPPR203.SPL (Песнь)
	Global("CDCLERIC_CHANT","LOCALS",0)
	Range("YXUNOMEI",15)
	!Global("YXUN_ATTACK","MYAREA",0)
THEN
	RESPONSE #100
		SetGlobal("CDCLERIC_CHANT","LOCALS",1)
		ForceSpell(Myself,CLERIC_CHANT)  // SPPR203.SPL (Песнь)
		Wait(2)
END

IF
	See(NearestEnemyOf(Myself))
	!BitCheck("CREATURE_STATE_VAR","LOCALS",8)
	!Global("YXUN_ATTACK","MYAREA",0)
THEN
	RESPONSE #100
		BitSet("CREATURE_STATE_VAR","LOCALS",8)
		Spell(LastSeenBy(Myself),CLERIC_HOLD_PERSON)
		Wait(2)
	RESPONSE #100
		BitSet("CREATURE_STATE_VAR","LOCALS",8)
		Spell(LastSeenBy(Myself),CLERIC_DISPEL_MAGIC)
		Wait(2)
	RESPONSE #100
		BitSet("CREATURE_STATE_VAR","LOCALS",8)
		Spell(LastSeenBy(Myself),CLERIC_GLYPH_OF_WARDING)
		Wait(2)
	RESPONSE #100
		BitSet("CREATURE_STATE_VAR","LOCALS",8)
		Spell(Myself,CLERIC_STRENGTH_OF_ONE)
		Wait(2)
END

IF
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		AttackReevaluate(LastSeenBy(Myself),75)
END

IF
	AttackedBy(NearestEnemyOf(Myself),DEFAULT)
THEN
	RESPONSE #100
		MoveToObject(LastAttackerOf(Myself))
END

IF
	!Global("YXUN_ATTACK","MYAREA",0)
	ActionListEmpty()
	!NearSavedLocation(Myself,"LOCALSSavedLoc",0)
THEN
	RESPONSE #100
		MoveToSavedLocation("SavedLoc","LOCALS")
END

IF
	!Global("YXUN_ATTACK","MYAREA",0)
	ActionListEmpty()
	!TargetUnreachable([PC])
THEN
  RESPONSE #100
    MoveToObject(Player1)
  RESPONSE #100
    NoAction()
END

IF
	ActionListEmpty()
	!NearSavedLocation(Myself,"LOCALSSavedLoc",0)
THEN
	RESPONSE #100
		MoveToSavedLocation("SavedLoc","LOCALS")
END

